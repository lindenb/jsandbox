options {
STATIC=false;
}


PARSER_BEGIN(PojoParser)

package sandbox.tools.pojogenerator.parser;
import java.util.List;
import java.math.BigInteger;
import java.util.ArrayList;
import java.io.*;
import sandbox.tools.pojogenerator.PojoGenerator;

    
@SuppressWarnings("unchecked")
public class PojoParser
	{
	private PojoGenerator generator = null;
	public PojoParser(PojoGenerator generator,final Reader r) {
		this(r);
		this.generator=generator;
		}
	}


PARSER_END(PojoParser)

SKIP :
{
" "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n"])* "\n">
| <"/*">: INSIDE_COMMENT
}

<INSIDE_COMMENT>
SKIP:
{
        <"*/">: DEFAULT
|       <~[]>
}


TOKEN : /* LITERALS */
{
  <CLASS: "class">
| <PACKAGE: "package">
| <LBRACKET: "{" >
| <RBRACKET: "}" >
| <LT: "<" >
| <GT: ">" >
| <COMMA: "," >
| <SEMICOLON: ";">
| <DOT: ".">
| <#LETTER: ["_","a"-"z","A"-"Z"] >
| <#DIGIT: ["0"-"9"] >
| <#SIGN: ["-","+"]>
| <#EXPONENT: ("E"|"e") (<SIGN>)? (<DIGIT>)+ >
| <FLOATING_NUMBER: (<DIGIT>)* "." (<DIGIT>)* (<EXPONENT>)?
| (<DIGIT>)+ (<EXPONENT>) >
| <INT_NUMBER: (<DIGIT>)+ >
| <MAP:( "java.util.Map" | "Map") >
| <LIST:( "java.util.List" | "List") >
| <IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
| <#ESCAPE_CHAR: "\\" ["n","t","b","r","f","\\","'","\""] >
| <SIMPLE_QUOTE_LITERAL: "\'" ( (~["\'","\\","\n","\r"]) | <ESCAPE_CHAR> )* "\'" >
| <DOUBLE_QUOTE_LITERAL: "\"" ( (~["\"","\\","\n","\r"]) | <ESCAPE_CHAR> )*"\"" >

}



public List<PojoGenerator.ClassDef> input(): { String packageName; PojoGenerator.ClassDef c;List<PojoGenerator.ClassDef> L = new ArrayList<PojoGenerator.ClassDef>();  }
    {
      c= classDecl() { L.add(c); }
    ( c= classDecl() { L.add(c); } )*
    <EOF>
    {return L;}
    }

private PojoGenerator.ClassDef classDecl():
    { String className; PojoGenerator.ClassDef def=null; List<PojoGenerator.ColumnDef> cols; }
    {
    (
    <CLASS> className=identifier() { def=generator.createClass(className);}
    <LBRACKET>
    ( cols=columnDecl() {def.columns.addAll(cols);})*
    <RBRACKET>
    )
    {return def;}
    }


private List<PojoGenerator.ColumnDef> columnDecl():
    {List<PojoGenerator.ColumnDef> cols=new ArrayList<PojoGenerator.ColumnDef>();PojoGenerator.Type t; List<String> names;}
    {
    (t=anyType() names=identifiers() <SEMICOLON>)
        {
        for(String n:names)
	        {
	        cols.add(generator.createColumn(n,t));
	        }
        return cols;
        }
    }

private List<String> identifiers():{List<String> L=new ArrayList<String>(); String s;}
	{
	(s=identifier() { L.add(s);} (<COMMA> s=identifier() { L.add(s);} )* )
		{
		return L;
		}
	}

private PojoGenerator.Type anyType():{PojoGenerator.Type p;} {
	(
	  p = listOf()
	| p = mapOf()
	| p = simpleType() 
	) {
		return p;
		}
	}

private PojoGenerator.Type listOf(): {PojoGenerator.Type p;} {
	<LIST> <LT> p=anyType() <GT> {
		return generator.createListOf(p);
		}
	}

private PojoGenerator.Type mapOf(): {PojoGenerator.Type p1,p2;} {
	<MAP> <LT> p1=anyType() <COMMA>  p2=anyType() <GT> {
		return generator.createMapOf(p1,p2);
		}
	}


private PojoGenerator.Type simpleType():
    {
    StringBuilder b=new StringBuilder();
    String s1;
    String s2;
    }
    {
    ( s1=identifier() { b.append(s1);} ( <DOT> s2=identifier() { b.append(".").append(s2);} )* )
        {
        return generator.createSimpleType(b.toString());
        }
    }

private String identifier():
    {
    Token t;
    }
    {
    t=<IDENTIFIER> { return t.image;}
    }


