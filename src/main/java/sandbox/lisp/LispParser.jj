options {
STATIC=false;
KEEP_LINE_COLUMN = true;
LOOKAHEAD = 3; // (JavaCC - default 1)
DEBUG_PARSER = false; // (JavaCC - default false)
DEBUG_TOKEN_MANAGER = false; // (JavaCC - default false)
}

// https://github.com/bkiers/Curta/blob/master/src/main/jjtree/CurtaParser.jjt
PARSER_BEGIN(LispParser)

package sandbox.lisp;
import java.util.List;
import java.util.ArrayList;
import java.math.BigDecimal;
import java.math.BigInteger;
import sandbox.lang.StringUtils;

    
@SuppressWarnings("unchecked")
public class LispParser {

private String toLocation(final Token t) {
	return "line "+t.beginLine+" column "+t.beginColumn+(t.beginColumn==t.endColumn?"":"-"+t.endColumn);
	}
private LispNode quote(LispNode n) {
	return LispPair.cons(LispSymbol.of("quote"),n);
	}
}

PARSER_END(LispParser)

SKIP :
{
" "
| "\t"
| "\n"
| "\r"
| <";" (~["\n"])* "\n">
}

TOKEN : /* LITERALS */
{
  <QOPAR:"'(">
| <OPAR:"(">
| <CPAR:")">
| <OARRAY:"[">
| <CARRAY:"]">
| <LEX_TRUE: "#t">
| <LEX_FALSE: "#f">
| <LEX_NIL: "null">
| <#LETTER: ["_","a"-"z","A"-"Z"] >
| <#DIGIT: ["0"-"9"] >
| <#SIGN: ["-","+"]>
| < DOT : "." >
| <#EXPONENT: ("E"|"e") (<SIGN>)? (<DIGIT>)+ >
| <FLOATING_NUMBER: (<DIGIT>)* "." (<DIGIT>)* (<EXPONENT>)?
| (<DIGIT>)+ (<EXPONENT>) >
| <INT_NUMBER: (<DIGIT>)+ >
| <#ESCAPE_CHAR: "\\" ["n","t","b","r","f","\\","'","\""] >
| <SIMPLE_QUOTE_LITERAL: "\'" ( (~["\'","\\","\n","\r"]) | <ESCAPE_CHAR> )* "\'" >
| <DOUBLE_QUOTE_LITERAL: "\"" ( (~["\"","\\","\n","\r"]) | <ESCAPE_CHAR> )*"\"" >
| <#OTHER: ~["'","(",")","[","]"," ","\n","\r"] >
| <#OTHERS: < OTHER > (< OTHER >)* >
| <QSYMBOL: "'" < OTHERS > >
| <SYMBOL: <OTHERS > > 
}


LispNode any(): {LispNode n=null;Token t=null;} {
	(
	  n = listNode()
	| n =  arrayNode()
	| n= atomNode()
	| n= quotedListNode()
	| t=<QSYMBOL > {
	  	return quote(LispSymbol.of(t.image.substring(1)));
	  	}
	| t=<SYMBOL > {
	  return LispSymbol.of(t.image);
	  }
	| < EOF > { return null; }	) {
	return n;
	}
	}

private LispNode quotedListNode(): {List<LispNode > L=new ArrayList<LispNode>();LispNode n1=null;LispNode n2=null;} {
	  < QOPAR > (n1=any() {L.add(n1);} )* <CPAR > {
		return quote(LispPair.of(L));
		}
	}

private LispPair listNode(): {List<LispNode > L=new ArrayList<LispNode >();LispNode n1=null;LispNode n2=null;} {
	(	  < OPAR > (n1=any() {L.add(n1);} )* <CPAR > {
		return LispPair.of(L);
		}
	| 	< OPAR > n1=any() < DOT > n2=any() < CPAR > {
			L.add(n1); 
			L.add(n2);
			return LispPair.of(L);
			}
	) 
	}


private LispArray arrayNode(): {List<Object> L=new ArrayList<Object>();LispAtom<?> n=null;} {
	< OARRAY > (n=atomNode() {L.add(n.getValue());} )* <CARRAY > {
		return LispArray.from(L);
		}
	}

private LispAtom<?> atomNode(): { Token t;}
	{
	( t=<LEX_TRUE>{
		return LispJavaObject.TRUE;
		}
	| t=<LEX_FALSE> {
		return LispJavaObject.FALSE;
		}
	| t=<LEX_NIL>{
		return LispJavaObject.NIL;
		}
	| t = <INT_NUMBER> {
		final BigInteger bi = new BigInteger(t.image);
		return LispJavaObject.of(bi);
		}
	| t= <FLOATING_NUMBER> {
		final BigDecimal bd = new BigDecimal(t.image);
		return LispJavaObject.of(bd);
		}
	| t= <SIMPLE_QUOTE_LITERAL> {
		return LispJavaObject.of(StringUtils.unquote(t.image));
		}
	| t= <DOUBLE_QUOTE_LITERAL> {
		return LispJavaObject.of(StringUtils.unquote(t.image));
		}
	
	)
	}
